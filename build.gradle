buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            url = "https://repo.spongepowered.org/repository/maven-public/"
        }
        maven {
            url = 'https://jitpack.io'
        }
    }
    dependencies {
        classpath('net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT')
        classpath('org.spongepowered:mixingradle:0.6-SNAPSHOT')
    }
}

plugins {
    id 'java'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "mixins.featheropt.refmap.json"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

group 'net.digitalingot'
version '1.0.0-SNAPSHOT'
archivesBaseName = 'FeatherOpt'

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    mappings = 'stable_22'
    runDir = project.file('run')
}

makeStart {
    doFirst {
        ext.clear = { arg -> addExtraLine('extras.clear();') }
        ext.extra = { arg -> addExtraLine('extras.add("' + arg + '");') }
        ext.tweak = { arg -> extra '--tweakClass'; extra arg }
        ext.mixin = { arg -> extra '--mixin'; extra arg }

        clear()
        tweak 'net.minecraftforge.fml.common.launcher.FMLTweaker'
        tweak 'net.minecraftforge.gradle.tweakers.CoremodTweaker'
        tweak 'org.spongepowered.asm.launch.MixinTweaker'
        mixin 'mixins.featheropt.json'
    }
}

afterEvaluate {
    tasks.sourceMainJava.replace '@MOD_VERSION@', project.version
}

repositories {
    mavenCentral()
    maven {
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        url = 'https://jitpack.io'
    }
}

dependencies {
    implementation group: 'org.jetbrains', name: 'annotations', version: '16.0.2'
    annotationProcessor "org.spongepowered:mixin:0.7.11-SNAPSHOT"
    testAnnotationProcessor "org.spongepowered:mixin:0.7.11-SNAPSHOT"
    implementation("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
    }
}

processResources {
    inputs.property 'version', { project.version }
}

jar {
    manifest {
        attributes 'MixinConfigs': 'mixins.featheropt.json',
                'FMLCorePluginContainsFMLMod': true,
                "ModSide": "CLIENT",
                'TweakClass': 'net.digitalingot.featheropt.FeatherOptTweaker',
                'TweakOrder': '0'
    }
}
